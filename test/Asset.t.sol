// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import {BaseTest} from "./Base.t.sol";
import {Asset} from "../src/Asset.sol";
import {Ownable} from "lib/openzeppelin-contracts/contracts/access/Ownable.sol";

contract AssetTest is BaseTest {
    function test_getAssetId() public view {
        assertEq(asset.getAssetId(), ASSET_ID);
    }

    function test_getRegistryAddress() public view {
        assertEq(asset.getRegistryAddress(), address(assetRegistry));
    }

    function test_getTokenAddress() public view {
        assertEq(asset.getTokenAddress(), address(testToken));
    }

    function test_getSubscriptionPrice() public view {
        assertEq(asset.getSubscriptionPrice(10), SUBSCRIPTION_PRICE * 10);
    }

    function _subscribe(uint256 duration) internal returns (uint256 subscription) {
        address owner = signer;
        address spender = address(asset);
        
        uint256 value = asset.getSubscriptionPrice(duration);

        uint256 deadline = block.timestamp + duration;
        
        (uint8 v, bytes32 r, bytes32 s) = getPermit(owner, spender, value, deadline);        

        subscription = asset.subscribe(owner, spender, value, deadline, v, r, s);
        
        return subscription;
    }

    function test_subscribe() public {

        uint256 tokenBalance = testToken.balanceOf(signer);

        vm.startPrank(signer);
        bool hasSubscription = asset.viewMySubscription();
        vm.stopPrank();
        if (hasSubscription) {
            return;
        }
        
        uint256 value = asset.getSubscriptionPrice(DURATION);

        vm.expectEmit(true, true, true, true);
        emit Asset.SubscriptionAdded(signer, block.timestamp, block.timestamp + DURATION, 0);

        uint256 subscription = _subscribe(DURATION);

        assertTrue(subscription > block.timestamp);

        vm.startPrank(signer);
        assertEq(asset.getMySubscription(), subscription);
        vm.stopPrank();

        uint256 assetBalance = testToken.balanceOf(address(asset));
        assertEq(assetBalance, value);
        assertEq(testToken.balanceOf(signer), tokenBalance - value);
    }

    function test_subscribe_multiple() public {

        uint256 deadline = block.timestamp;

        uint256 count = 10;

        for (uint256 i = 0; i < count; i++) {
            vm.expectEmit(true, true, true, true);
            emit Asset.SubscriptionAdded(signer, deadline, deadline + DURATION, i);
            _subscribe(DURATION);
            deadline += DURATION;
        }

        vm.startPrank(signer);
        assertEq(asset.getMySubscription(), block.timestamp + (DURATION * count));
        vm.stopPrank();
    }

    function test_subscribe_multiple_subscriptionPrice() public {
        uint256 tokenBalance = testToken.balanceOf(signer);
        
        _subscribe(DURATION);

        uint256 value = asset.getSubscriptionPrice(DURATION);

        vm.startPrank(assetOwner);
        asset.setSubscriptionPrice(SUBSCRIPTION_PRICE * 2);
        vm.stopPrank();

        _subscribe(DURATION);

        value += asset.getSubscriptionPrice(DURATION);

        assertEq(value, 3 * (SUBSCRIPTION_PRICE * DURATION));
        assertEq(testToken.balanceOf(signer), tokenBalance - value);
    }

    function test_claimCreatorFee() public {
        test_subscribe();

        uint256 value = asset.getSubscriptionPrice(DURATION);
        vm.warp(block.timestamp + DURATION);

        vm.startPrank(assetOwner);
        uint256 creatorFee = assetRegistry.getCreatorFee(value);
        vm.expectEmit(true, true, true, true);
        emit Asset.CreatorFeeClaimed(signer, creatorFee);
        uint256 claimedCreatorFee = asset.claimCreatorFee(signer);
        vm.stopPrank();

        assertEq(claimedCreatorFee, creatorFee);
        assertEq(testToken.balanceOf(assetOwner), claimedCreatorFee);
    }

    function test_claimCreatorFee_multiple() public {
        test_subscribe_multiple();

        vm.startPrank(signer);

        uint256 endTime = asset.getMySubscription();
        uint256 value = asset.getSubscriptionPrice(endTime - block.timestamp);
        
        vm.stopPrank();

        vm.warp(endTime);

        vm.startPrank(assetOwner);

        uint256 creatorFee = assetRegistry.getCreatorFee(value);
        vm.expectEmit(true, true, true, true);
        emit Asset.CreatorFeeClaimed(signer, creatorFee);
        
        uint256 claimedCreatorFee = asset.claimCreatorFee(signer);
        
        vm.stopPrank();
        
        assertEq(claimedCreatorFee, creatorFee);
        assertEq(testToken.balanceOf(assetOwner), claimedCreatorFee);
    }

    function test_claimCreatorFee_multiple_subscriptionPrice() public {
        uint256 tokenBalance = testToken.balanceOf(assetOwner);
        
        _subscribe(DURATION);

        uint256 value = asset.getSubscriptionPrice(DURATION);

        vm.startPrank(assetOwner);
        asset.setSubscriptionPrice(SUBSCRIPTION_PRICE * 2);
        vm.stopPrank();

        _subscribe(DURATION);
        
        value += asset.getSubscriptionPrice(DURATION);

        uint256 creatorFee = assetRegistry.getCreatorFee(value);

        vm.warp(block.timestamp + (DURATION * 2));

        vm.startPrank(assetOwner);
        vm.expectEmit(true, true, true, true);
        emit Asset.CreatorFeeClaimed(signer, creatorFee);
        uint256 claimedCreatorFee = asset.claimCreatorFee(signer);
        vm.stopPrank();

        assertEq(claimedCreatorFee, creatorFee);
        assertEq(testToken.balanceOf(assetOwner), tokenBalance + claimedCreatorFee);
    }

    function test_claimCreatorFee_midSubscription() public {
        uint256 tokenBalance = testToken.balanceOf(assetOwner);
        test_subscribe();
        
        uint256 value = asset.getSubscriptionPrice(DURATION);
        vm.warp(block.timestamp + (DURATION / 2));
    
        vm.startPrank(assetOwner);
        uint256 creatorFee = assetRegistry.getCreatorFee(value) / 2;
        vm.expectEmit(true, true, true, true);
        emit Asset.CreatorFeeClaimed(signer, creatorFee);
        uint256 claimedCreatorFee = asset.claimCreatorFee(signer);
        vm.stopPrank();

        assertEq(claimedCreatorFee, creatorFee);
        assertEq(testToken.balanceOf(assetOwner), tokenBalance + claimedCreatorFee);
    }

    function test_claimCreatorFee_startOfNextSubscription() public {
        uint256 tokenBalance = testToken.balanceOf(assetOwner);
        
        uint256 endTime = _subscribe(DURATION);
        
        uint256 value = asset.getSubscriptionPrice(DURATION);
        
        _subscribe(DURATION);

        vm.warp(endTime);
        
        vm.startPrank(assetOwner);
        uint256 creatorFee = assetRegistry.getCreatorFee(value);
        vm.expectEmit(true, true, true, true);
        emit Asset.CreatorFeeClaimed(signer, creatorFee);
        uint256 claimedCreatorFee = asset.claimCreatorFee(signer);
        vm.stopPrank();

        assertEq(claimedCreatorFee, creatorFee);
        assertEq(testToken.balanceOf(assetOwner), tokenBalance + claimedCreatorFee);
    }

    function test_setSubscriptionPrice() public {
        
        uint256 newPrice = 200;

        vm.startPrank(assetOwner);
        vm.expectEmit(true, true, true, true);
        emit Asset.SubscriptionPriceUpdated(newPrice);
        asset.setSubscriptionPrice(newPrice);
        vm.stopPrank();

        assertEq(asset.getSubscriptionPrice(1), newPrice);
    }

    function test_revokeSubscription() public {
        
        uint256 tokenBalance = testToken.balanceOf(signer);
        
        _subscribe(DURATION);

        uint256 value = asset.getSubscriptionPrice(DURATION);

        assertEq(testToken.balanceOf(signer), tokenBalance - value);

        vm.startPrank(assetOwner);
        vm.expectEmit(true, true, true, true);
        emit Asset.SubscriptionRevoked(signer);
        asset.revokeSubscription(signer);
        vm.stopPrank();

        // should be the same as before the subscription because of reimbursing the subscription price
        assertEq(testToken.balanceOf(signer), tokenBalance);

        vm.startPrank(signer);
        assertEq(asset.getMySubscription(), 0);
        vm.stopPrank();
    }

    function test_revokeSubscription_multiple() public {
        uint256 tokenBalance = testToken.balanceOf(signer);

        test_subscribe_multiple();

        uint256 value = asset.getSubscriptionPrice(DURATION * 10);

        assertEq(testToken.balanceOf(signer), tokenBalance - value);

        vm.startPrank(assetOwner);
        vm.expectEmit(true, true, true, true);
        emit Asset.SubscriptionRevoked(signer);
        asset.revokeSubscription(signer);
        vm.stopPrank();

        assertEq(testToken.balanceOf(signer), tokenBalance);

        vm.startPrank(signer);
        assertEq(asset.getMySubscription(), 0);
        vm.stopPrank();
    }

    function test_revokeSubscription_midSubscription() public {
        uint256 tokenBalance = testToken.balanceOf(signer);


        uint256 count = 2;
        for (uint256 i = 0; i < count; i++) {
            _subscribe(DURATION);
        }

        uint256 value = asset.getSubscriptionPrice(DURATION);

        vm.warp(block.timestamp + DURATION + (DURATION / 2));

        vm.startPrank(assetOwner);
        asset.revokeSubscription(signer);
        vm.stopPrank();

        assertEq(testToken.balanceOf(signer), tokenBalance - (value + (value / 2)));

        vm.startPrank(signer);
        assertEq(asset.getMySubscription(), block.timestamp);
        vm.stopPrank();
    }

    function test_revokeSubscription_endOfSubscription() public {
        uint256 tokenBalance = testToken.balanceOf(signer);

        uint256 endTime = _subscribe(DURATION);

        uint256 value = asset.getSubscriptionPrice(DURATION);

        vm.warp(block.timestamp + DURATION);

        vm.startPrank(assetOwner);
        asset.revokeSubscription(signer);
        vm.stopPrank();

        assertEq(testToken.balanceOf(signer), tokenBalance - value);

        vm.startPrank(signer);
        assertEq(asset.getMySubscription(), block.timestamp);
        vm.stopPrank();
    }

    function test_revokeSubscription_multiple_subscriptionPrice() public {
        uint256 tokenBalance = testToken.balanceOf(signer);

        _subscribe(DURATION);

        uint256 value = asset.getSubscriptionPrice(DURATION);

        vm.startPrank(assetOwner);
        asset.setSubscriptionPrice(SUBSCRIPTION_PRICE * 2);
        vm.stopPrank();

        _subscribe(DURATION);

        value += asset.getSubscriptionPrice(DURATION);

        vm.startPrank(assetOwner);
        asset.revokeSubscription(signer);
        vm.stopPrank();

        vm.startPrank(signer);
        assertEq(asset.getMySubscription(), 0);
        vm.stopPrank();

        assertEq(value, DURATION * SUBSCRIPTION_PRICE * 3);
        assertEq(testToken.balanceOf(signer), tokenBalance);
    }

    function test_viewSubscription() public {
        
        test_subscribe();

        vm.startPrank(signer);
        assertEq(asset.viewMySubscription(), true);
        vm.stopPrank();

        vm.startPrank(assetOwner);
        asset.revokeSubscription(signer);
        vm.stopPrank();

        vm.startPrank(signer);
        assertEq(asset.viewMySubscription(), false);
        vm.stopPrank();
    }

    function test_unauthorized() public {
        vm.startPrank(address(403));

        vm.expectRevert(Asset.OnlyRegistryOrOwnerUnauthorizedAccount.selector);
        asset.getSubscription(signer);

        vm.expectRevert(Asset.OnlyRegistryOrOwnerUnauthorizedAccount.selector);
        asset.viewSubscription(signer);

        vm.stopPrank();
    }

    function test_subscribe_invalidSpender() public {
        address owner = signer;
        address spender = address(1); // Wrong spender - must be address(asset)
        uint256 value = asset.getSubscriptionPrice(DURATION);
        uint256 deadline = block.timestamp + DURATION;
        (uint8 v, bytes32 r, bytes32 s) = getPermit(owner, address(asset), value, deadline);

        vm.expectRevert(Asset.InvalidSpender.selector);
        asset.subscribe(owner, spender, value, deadline, v, r, s);
    }

    function test_subscribe_permitFailed() public {
        address owner = signer;
        address spender = address(asset);
        uint256 value = asset.getSubscriptionPrice(DURATION);
        uint256 deadline = block.timestamp + DURATION;
        // Use invalid signature - wrong v, r, s
        (uint8 v, bytes32 r, bytes32 s) = (0, bytes32(0), bytes32(0));

        vm.expectRevert(Asset.PermitFailed.selector);
        asset.subscribe(owner, spender, value, deadline, v, r, s);
    }

    function test_subscribe_insufficientFunds() public {
        address owner = signer;
        address spender = address(asset);
        uint256 value = SUBSCRIPTION_PRICE - 1; // Below subscriptionPrice, rounds to 0
        uint256 deadline = block.timestamp + DURATION;
        (uint8 v, bytes32 r, bytes32 s) = getPermit(owner, spender, value, deadline);

        vm.expectRevert(Asset.InsufficientFunds.selector);
        asset.subscribe(owner, spender, value, deadline, v, r, s);
    }

    function test_setSubscriptionPrice_unauthorized() public {
        vm.startPrank(address(403));
        vm.expectRevert(abi.encodeWithSelector(Ownable.OwnableUnauthorizedAccount.selector, address(403)));
        asset.setSubscriptionPrice(200);
        vm.stopPrank();
    }

    function test_revokeSubscription_unauthorized() public {
        test_subscribe();

        vm.startPrank(address(403));
        vm.expectRevert(abi.encodeWithSelector(Ownable.OwnableUnauthorizedAccount.selector, address(403)));
        asset.revokeSubscription(signer);
        vm.stopPrank();
    }

    function test_revokeSubscription_noSubscription() public {
        vm.startPrank(assetOwner);
        vm.expectRevert(Asset.SubscriptionNotFound.selector);
        asset.revokeSubscription(signer);
        vm.stopPrank();
    }

    function test_feeSplit() public {
        uint256 creatorBalance = testToken.balanceOf(assetOwner);
        uint256 registryBalance = testToken.balanceOf(registryOwner);
        
        test_subscribe();

        uint256 value = asset.getSubscriptionPrice(DURATION);

        (uint256 creatorFee, uint256 registryFee) = assetRegistry.getFees(value);

        vm.startPrank(signer);
        uint256 subscriptionEndTime = asset.getMySubscription();
        vm.warp(subscriptionEndTime);
        vm.stopPrank();

        vm.startPrank(assetOwner);
        uint256 claimedCreatorFee = asset.claimCreatorFee(signer);
        vm.stopPrank();

        vm.startPrank(address(assetRegistry));
        uint256 claimedRegistryFee = asset.claimRegistryFee(signer);
        vm.stopPrank();

        assertEq(claimedCreatorFee, creatorFee);
        assertEq(claimedRegistryFee, registryFee);

        assertEq(testToken.balanceOf(assetOwner), creatorBalance + creatorFee);
        assertEq(testToken.balanceOf(registryOwner), registryBalance + registryFee);
    }
}